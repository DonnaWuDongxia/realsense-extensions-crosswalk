# Copyright (c) 2016 Intel Corporation. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//xwalk/common/xwalk_common.gni")

xwalk_idlgen("face_module_idl") {
  namespace = "realsense::jsapi"
  sources = [ "face_module.idl" ]
}

xwalk_js2c("face_js") {
  sources = [ "../js/face_api.js" ]
}

shared_library("face") {
  sources = [
    "face_extension.cc",
    "face_extension.h",
    "face_instance.cc",
    "face_instance.h",
    "face_module_object.cc",
    "face_module_object.h",
  ]
  deps = [
    "../../common:common_idl",
    "../../common:common_utils",
    ":face_module_idl",
    ":face_js",
    "//extensions/third_party/libpxc",
    "//xwalk/common:common_static",
  ]
  include_dirs = [
    "../../..",
    "$target_gen_dir",
  ]
}

copy("npm_package") {
  sources = [ "../npm/README.md", "../npm/package.json" ]
  dist_dir = "$root_build_dir/realsense_extensions/face"
  outputs = [ "$dist_dir/{{source_file_part}}" ]
}

action("make_dist") {
  script = "//extensions/realsense/common/dist.py"
  dll_file = "face.dll"
  sources = [
    "../../common/XWalkExtensionHooks_template.js",
    "../../common/npm_install_template.js",
  ]
  dist_dir = "$root_build_dir/realsense_extensions/face"
  outputs = [
    "$dist_dir/$dll_file",
    "$dist_dir/XWalkExtensionHooks.js",
    "$dist_dir/npm_install.js",
  ]
  args = [
    "--extension-dll", rebase_path("$root_build_dir/$dll_file"),
    "--target-dir", rebase_path("$dist_dir"),
  ]
  deps = [ ":face", ":npm_package" ]
}
